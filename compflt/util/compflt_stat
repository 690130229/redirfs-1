#!/bin/sh

file=/proc/fs/compflt/stat
tmpf=/tmp/compflt_stat
cp $file $tmpf

awk -- '
BEGIN {
        print "==== per-file stats ===";
}
/^[^[]/ {
        compt=$3+$5;
        per_alg_compt[$2] += compt;
        per_alg_decomp[$2] += $4;
	ratio=100 - compt/$4*100;
        print "<" $1 ">" "\t" $2 "\t" $4 " -> " compt " (" ratio "%)";
}
END {
        print "==== per-alg stats ===";
        for (alg in per_alg_compt) {
                compt = per_alg_compt[alg];
                decomp = per_alg_decomp[alg];
                all["compt"] += compt;
                all["decomp"] += decomp;
                ratio = 100 - compt/decomp*100;
                print "<" alg ">   \t" decomp " -> " compt " (" ratio "%)";
        }
        avg = 100 - all["compt"] / all["decomp"] * 100;
        print "\navg ratio achieved: " avg "%";
}
' $tmpf
